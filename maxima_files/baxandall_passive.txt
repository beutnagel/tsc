/* Maxima batch file for solving circuit transfer function from matrix representation  */
/* in symbolic form using Cramer's rule. The numerator and denominator expressions are */
/* evaluated separately as polynomial coefficients in powers of complex variable s.    */
/* Jarmo L. 14.9.2018 */

/* This example solves the coefficients for a passive Baxandall tone control  */
/* circuit as Vout/Vin. Details of the circuit analysis and matrix algebra    */
/* are available in a separate document.                                      */
/* Charles Day 26.4.2021 */

stardisp: true$           /* multiplication is displayed with an asterisk between operands */
ratmx: true$              /* matrix operations  are performed in CRE */
/* matrix obtained from current loops in the circuit */
matrix(
    [RIN+R1+RB1+RB2+R2, -R1, -RB1, -RB2, -R2, 0],
    [-R1, R1+R3+RT1+1/(s*C1)+1/(s*C3), -1/(s*C1), 0, -R3, -1/(s*C3)],
    [-RB1, -1/(s*C1), RB1+1/(s*C1), 0, 0, 0],
    [-RB2, 0, 0, RB2+1/(s*C2), -1/(s*C2), 0],
    [-R2, -R3, 0, -1/(s*C2), R2+R3+RL+1/(s*C2), -RL],
    [0, -1/(s*C3), 0, 0, -RL, RT2+RL+1/(s*C3)]
)$
determinant(%)$           /* evaluates the determinant of the previous output % */
DEN_fs: factor(%)$        /* expands the result and factors to common denominator, save to variable DEN_fs */
DEN_den: denom(DEN_fs)$   /* saves a variable DEN_den for denominator expression */ 
pld: num(DEN_fs)$         /* saves a variable pld for numerator expression */
/* extract denominator coefficients in the powers of s to designated variables */
DEN_XRe: coeff(pld,s,4);
DEN_AIm: coeff(pld,s,3);
DEN_BRe: coeff(pld,s,2);
DEN_CIm: coeff(pld,s,1);
DEN_DRe: coeff(pld,s,0);

/* matrices to solve Vout/Vin using Cramer's rule */
matrix(
    [-R1, R1+R3+RT1+1/(s*C1)+1/(s*C3), -1/(s*C1), 0, -1/(s*C3)],
    [-RB1, -1/(s*C1), RB1+1/(s*C1), 0, 0],
    [-RB2, 0, 0, RB2+1/(s*C2), 0],
    [-R2, -R3, 0, -1/(s*C2), -RL],
    [0, -1/(s*C3), 0, 0, RT2+RL+1/(s*C3)]
)$
M1_5: determinant(%)$
matrix(
    [-R1, R1+R3+RT1+1/(s*C1)+1/(s*C3), -1/(s*C1), 0, -R3],
    [-RB1, -1/(s*C1), RB1+1/(s*C1), 0, 0],
    [-RB2, 0, 0, RB2+1/(s*C2), -1/(s*C2)],
    [-R2, -R3, 0, -1/(s*C2), R2+R3+RL+1/(s*C2)],
    [0, -1/(s*C3), 0, 0, -RL]
)$
M1_6: determinant(%)$
M1_5+M1_6$
NUM_fs: factor(%)$
NUM_den: denom(NUM_fs)$
/* expand to common denominator for numerator and denominator in Cramer's division */
pln: expand(RL*num(NUM_fs)*DEN_den/NUM_den)$
/* extract numerator coefficients in the powers of s to designated variables */
NUM_XRe: coeff(pln,s,4);
NUM_AIm: coeff(pln,s,3);
NUM_BRe: coeff(pln,s,2);
NUM_CIm: coeff(pln,s,1);
NUM_DRe: coeff(pln,s,0);
DEN_den;
NUM_den;
DEN_den/NUM_den;
