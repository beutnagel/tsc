/* Maxima batch file for solving circuit transfer function from matrix representation  */
/* in symbolic form using Cramer's rule. The numerator and denominator expressions are */
/* evaluated separately as polynomial coefficients in powers of complex variable s.    */
/* This example solves the coefficients for   Aria   tone control circuit as Vout/Vin  */
/* Jarmo L. 14.9.2018 */

stardisp: true$           /* multiplication is displayed with an asterisk between operands */
ratmx: true$              /* matrix operations are performed in CRE */
ZT: s*CT1/(1 + s*CT1*RT1)$
/* matrix obtained from current loops in the circuit */
matrix(
  [1/RIN+ZT+1/RM1+1/RB3,        -ZT         ,             0              ,     -1/RM1      ,        0        ,     -1/RB3      ],
  [        -ZT         ,ZT+1/RT2+1/RT3+s*CT2,        -1/RT3-s*CT2        ,        0        ,        0        ,        0        ],
  [         0          ,    -1/RT3-s*CT2    ,1/RL+s*CT2+1/RT3+1/RM3+1/RB4,      -1/RM3     ,     -1/RB4      ,        0        ],
  [       -1/RM1       ,         0          ,           -1/RM3           ,1/RM1+1/RM2+1/RM3,        0        ,        0        ],
  [         0          ,         0          ,           -1/RB4           ,        0        ,1/RB1+1/RB2+1/RB4,     -1/RB1      ],
  [       -1/RB3       ,         0          ,             0              ,        0        ,     -1/RB1      ,1/RB1+1/RB3+s*CB1]
)$
determinant(%)$           /* evaluates the determinant of the previous output % */
DEN_fs: factor(%)$        /* expands the result and factors to common denominator, save to variable DEN_fs */
DEN_den: denom(DEN_fs)$   /* saves a variable DEN_den for denominator expression */ 
pld: num(DEN_fs)$         /* saves a variable pld for numerator expression */
/* extract denominator coefficients in the powers of s to designated variables */
DEN_XRe: coeff(pld,s,4);
DEN_AIm: coeff(pld,s,3);
DEN_BRe: coeff(pld,s,2);
DEN_CIm: coeff(pld,s,1);
DEN_DRe: coeff(pld,s,0);

/* matrix to solve Iout using Cramer's rule */
matrix(
  [1/RIN+ZT+1/RM1+1/RB3,        -ZT         ,           1/RIN            ,     -1/RM1      ,        0        ,     -1/RB3      ],
  [        -ZT         ,ZT+1/RT2+1/RT3+s*CT2,             0              ,        0        ,        0        ,        0        ],
  [         0          ,    -1/RT3-s*CT2    ,             0              ,      -1/RM3     ,     -1/RB4      ,        0        ],
  [       -1/RM1       ,         0          ,             0              ,1/RM1+1/RM2+1/RM3,        0        ,        0        ],
  [         0          ,         0          ,             0              ,        0        ,1/RB1+1/RB2+1/RB4,     -1/RB1      ],
  [       -1/RB3       ,         0          ,             0              ,        0        ,     -1/RB1      ,1/RB1+1/RB3+s*CB1]
)$
determinant(%)$
NUM_fs: factor(%)$
NUM_den: denom(NUM_fs)$
pln: expand(num(NUM_fs)*DEN_den/NUM_den)$  /* expand to common denominator for numerator and denominator in Cramer's division */
/* extract numerator coefficients in the powers of s to designated variables */
NUM_XRe: coeff(pln,s,4);
NUM_AIm: coeff(pln,s,3);
NUM_BRe: coeff(pln,s,2);
NUM_CIm: coeff(pln,s,1);
NUM_DRe: coeff(pln,s,0);
DEN_den;
NUM_den;
DEN_den/NUM_den;


